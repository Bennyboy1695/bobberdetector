plugins {
    id 'com.github.johnrengelman.shadow'
}

loom {
    var common = project(":common")
    accessWidenerPath = common.loom.accessWidenerPath
    runs {
        client {
            vmArg "-XX:+AllowEnhancedClassRedefinition"
            programArgs "--username=Dev"
        }
        server {
            vmArg "-XX:+AllowEnhancedClassRedefinition"
        }
        data {
            data()
            name "Data Generation"
            programArgs "--all", "--mod", "bobberdetector"
            programArgs "--output", file("src/generated/resources").absolutePath
            programArgs "--existing", project(":common").file("src/main/resources").absolutePath
            programArgs "--existing-mod", "create"
        }
    }
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:${neoforge_version}"
    // Create and its dependencies
    modImplementation("com.simibubi.create:create-${minecraft_version}:${create_neoforge_version}:slim") { transitive = false }
    modImplementation("com.tterrag.registrate:Registrate:${registrate_neoforge_version}") {transitive = false}
    modImplementation("dev.engine-room.flywheel:flywheel-neoforge-${minecraft_version}:${flywheel_neoforge_version}")
    modImplementation("net.createmod.ponder:Ponder-NeoForge-${minecraft_version}:${ponder_version}") { transitive = false}

    modLocalRuntime("curse.maven:jade-324717:${jade_neoforge_curse_id}")
    modLocalRuntime("mezz.jei:jei-${minecraft_version}-neoforge:${jei_neoforge_version}")

    compileOnly("io.github.llamalad7:mixinextras-common:${mixin_extras_version}")
    annotationProcessor(implementation(include("io.github.llamalad7:mixinextras-neoforge:${mixin_extras_version}")))

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')
}

processResources {
    inputs.property 'version', rootProject.version

    filesMatching('META-INF/neoforge.mods.toml') {
        expand version: rootProject.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev'
}

remapJar {
    atAccessWideners.add("bobberdetector.accesswidener")
    inputFile.set(shadowJar.archiveFile)
    dependsOn shadowJar
    archiveClassifier.set(null)
}

sourceSets {
    main {
        resources.srcDir 'src/generated/resources'
    }
}
